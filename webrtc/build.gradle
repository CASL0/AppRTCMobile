plugins {
	id 'com.android.library'
	id 'kotlin-android'
	id 'kotlin-kapt'
}

android {
	compileSdk versionCompiler

	compileOptions {
		sourceCompatibility javaSourceCompatibility
		targetCompatibility javaTargetCompatibility
	}

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion versionTarget
		versionCode versionCodeNum
		versionName versionNameString

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	kotlinOptions.jvmTarget = "1.8"
}

dependencies {
	testImplementation "junit:junit:${junitVersion}"
	androidTestImplementation "androidx.test.ext:junit:${androidXTestRunnerVersion}"
	androidTestImplementation "androidx.test.espresso:espresso-core:${androidXEspressoVersion}"

	implementation "androidx.appcompat:appcompat:${androidXAppCompat}"
	implementation "androidx.legacy:legacy-support-v4:${androidXLegacyV4}"
//	implementation "com.google.android.material:material:${androidXMaterial}"

	implementation "com.serenegiant:common:${commonLibVersion}"

	implementation project(':libwebrtc')
}

def repo = new File(rootDir, "repository")

apply plugin: 'maven-publish'

afterEvaluate {
	publishing {
		repositories {
			maven {
				url = "file://${repo.absolutePath}"
			}
		}
		publications {
			release(MavenPublication) {
				groupId = 'com.serenegiant'
				artifactId = 'webrtc'
				version = webrtcAarVersionString
				from components.release
				pom {
					inceptionYear = '2018'
					licenses {
						license {
							name = 'The WebRTC Software License'
							url = 'https://webrtc.org/support/license'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'saki4510t'
							name = 't_saki'
							email = 't_saki@serenegiant.com'
						}
					}
				}
			}
		}
	}
}
